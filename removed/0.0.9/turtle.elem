.{? type: turtle
    A turtle. Used for drawing patterns on a plot grid.
.}
{@turtle,
  attr "x y r path"

  {[0 0 pi2/ [[0 0]]] turtle MO}:new;
  {self, "{turtle:$(self.x),$(self.y),$(self.r rad)\{deg}}"}:show;

  .#? N turtle.fwd\n  move the turtle forward N units
  {len self,
    self.x self.r Mc len* + self.setx
    self.y self.r Ms len* + self.sety
    [self.x self.y] self.path .V;
  }:fwd;

  .#? N turtle.left\n  turn the turtle left N radians
  {rads self,
    self.r rads + self.setr
  }:left;

  .#? N turtle.right\n  turn the turtle right N radians
  {rads self,
    self.r rads - self.setr
  }:right;

  .#? turtle.draw\n  draw the turtle's path
  {self : x y p small big margin,
    plot!:p;
    self.path transpose ~\ :x;:y;
    x p.domain
    y p.addline

    [x max y max] max :big;
    [x min y min] min :small;
    big 0.1* :margin;

    small margin- big margin+ p.setxaxis
    small margin- big margin+ p.setyaxis

    p.view

  }:draw;
}



{@turtledemo,

  .#? turtledemo.ribbon\n  a ribbon
  {:frank,
    turtle! :frank;
    0 :count;
    {
      1 frank.fwd
      count Ms frank.right
      0.01 count +: count;
    } 1200 %
    frank.draw
  }:ribbon;

  .#? turtledemo.flowe\n  a simple flower
  {:t,
    turtle!:t;
    for 'i (12R) {
      for 'j (12R) {
        1 t.fwd
        30째 t.right
      };
      30째 t.right
    };
    t.draw
  }:flower;

  .#? turtledemo.snowflake\n  a snowflake
  {:t,
    turtle!:t;
    for 'i (24R) {
      for 'j (36R) {
        1 t.fwd
        170째 t.right
      };
      20째 t.right
    };
    t.draw
  }:snowflake;

}
