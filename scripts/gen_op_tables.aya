.# This file generates the tables on the aya wiki

import ::dataframe

.# Type Key
{, ["Type" "Abbreviation"]:columns } dataframe! :tc;

0 ["Number" 'N] tc.setrow
1 ["String" 'S] tc.setrow
2 ["Char" 'C] tc.setrow
3 ["Block" 'B] tc.setrow
4 ["Dict" 'D] tc.setrow
5 ["Symbol" 'J] tc.setrow

{,1:noindex} tc.tohtml
"<h2>Type Key</h2>" \+ :typekey;



{title ops : opdf count(0),

  {,
    ["name" "types" "info" "overload"]:columns;
  } dataframe! :opdf;

  ops # {op,
    count
    [ op.name
      op.types .EVVRI
      op.info
      op [::overload ""] :I \;]
    opdf.setrow
    count B :count;
  };

  "\n<h2>$title</h2><br/>\n"
  {, 1:noindex} opdf.tohtml +
}:gentable;


[
  "Basic Operators"
  "Dot Operators"
  "Colon Operators"
  "Misc Operators"
]:names;

4.R # {i,
  names i I i M? gentable
} S

typekey \+

"""*This page is dynamically generated by the [scripts/gen_op_tables.aya](https://github.com/nick-paul/aya-lang/blob/master/scripts/gen_op_tables.aya) script.*
"""
\+

"docs/ophtml.html" 0 .G
