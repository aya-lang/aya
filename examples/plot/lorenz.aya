.# Plot a Lorenz attractor
import ::plot

.# Starting parameters

0.01 :x;
0.1  :y;
0.3  :z;
10   :a;
28   :b;
8 3 / :c;
0.005 :dt;

.# List to keep track of state
[]

{
  .# Update the point
  [
    y x - a * dt* x+:x
    b z - x * y - dt* y+:y
  ]
  x y * c z * - dt* z+:z;

  .# Append to state list
  \.B
} 50000 %

.# Transpose to separate list of x and y values
.T ~ plot.line:plt;

"Lorenz Attractor" plt.:title;
0.3 plt.:stroke;

plt.view

.# Lorenz attractor golfed (61 chars)
{
    import "golf"

    -2E:t:α10*Y3*X{yα-d*t*α+:α28x-α*y-t*y+YJ$~*3x*-t*x+X}4E%.A.T.x
};
