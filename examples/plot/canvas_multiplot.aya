import ::canvas

20 :gap;
{,
	800 gap + :width
	800 gap + :height
	"Canvas Multiplot" :name
} canvas! :c;

.{ Add a box-plot in the top-left corner .}
{,
	0 :show
	{,
		c.id :id
		0 :x
		0 :y
	} :canvas
	400 :width :height
	[0 1 2 1 0] # {x, {, x 5* 10+ .R :values}} :data
} :{plot.box_plot}

.{ Add a pie-chart in the bottom-left corner .}
{,
	0 :show
	{,
		c.id :id
		0 :x
		400 gap + :y
	} :canvas
	400 :width :height
	[1 3 6] :data
} :{plot.pie_chart}

.{ Add a multiplot in the right half .}
{,
	0 :show
	{,
		c.id :id
		400 gap + :x
		0 :y
	} :canvas
	400 :width
	800 gap + :height
	[
		{,
			[
				{, 10.R :x #{3*} :y "3*" :label}
			] :data
		}
		{,
			[
				{, 10.R :x #{3*} :y "3*" :label}
				{, 10.R :x #{4*} :y "4*" :label}
			] :data
		}
		{,
			[
				{, 10.R :x #{3*} :y "3*" :label}
				{, 10.R :x #{4*} :y "4*" :label}
				{, 10.R :x #{5*} :y "5*" :label}
			] :data
		}
	] :subplots
} :{plot.multiplot}

c.show
c.wait