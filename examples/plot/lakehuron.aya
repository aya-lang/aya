.# A script demonstrating the use of dataframes, plots, and
.#   retrieving information from the web. 

import ::dataframe
import ::plot
import ::stats

"Downloading file..." :P
:{
    "https://raw.githubusercontent.com/vincentarelbundock/Rdatasets/master/csv/datasets/LakeHuron.csv":filename
    1:csvindex
}
dataframe.read_csv :df;

plot.plot! :plt;

df.["time"] :x;
df.["value"] :y;

.# stats.regression returns a function
x y stats.regression :r;

x y   :{ "Water Level":label} plt.plot
x {r} :{ "Trend":label} plt.plot

"Water Level of Lake Huron" plt.:title;
[575 583] plt.y.:lim;
1 plt.y.:gridlines;
"Water Level" plt.y.:label
"Year" plt.x.:label
"####" plt.x.:numberformat;
2 plt.:stroke;

plt.view
