import ::canvas
import ::time
import ::color

.# https://processing.org/examples/keyboardfunctions.html
.# Based off the original 'Color Typewriter' concept by John Maeda.

12 :num_tiles;  .# Number of tiles on the canvas
40 :tile_width; .# width of each tile

.# List to store tiles in
color.colors.white num_tiles 2^ L :grid;
0:index;

{,
    num_tiles :width
    num_tiles :height
    tile_width :scale
} canvas! :cvs;
cvs.show

60 time.rate! :limiter;
{
    cvs.typed_chars :# {ch,
        .# Tile hue is based of its key code
        (ch :' 23* 360%, 0.8, 0.8) color.newhsv grid.:[index num_tiles 2^ %];
        index 1+ :index;
    };

    cvs.clear

    .# Draw the grid
    grid.enumerate :# {e : index color x y,
      e.[0] :index;
      e.[1] :color;

      index num_tiles %  :x;
      index num_tiles .% :y;

      color cvs.set_color
      x y cvs.point
    };

    cvs.show
    limiter.sleep
    cvs.isopen
} W
