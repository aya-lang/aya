import turtle
import canvas
require color {color}

.# Draw a pattern using the turtle library
.# Based on the example in Luxor.jl
.# https://juliagraphics.github.io/Luxor.jl/stable/turtle/

.# The canvas to pass to the turtle
:{ 500:width; 500:height; } canvas.canvas! :cvs;

def ::newimage { : cvs^ ,

    :{ cvs :canvas; "#444" color.fromstr :bg_color; } turtle.turtle! :t;
    .# random bright color
    360Q 1.0 1.0 color.newhsv :c;
    .# Random number between 60 and 190
    60 190 \$@\-Q+ :dtheta;
    .Q :dstep;
    0 :n;
    .Q :shift;
    {
        shift c.hueshift :c;
        c t.pencolor
        n dstep + :n;
        dtheta t.right
        n t.fd
    } 400 %
}

.# Generate new images forever
{
    newimage
    "Press enter to generate a new image. Enter 'q' to quit" :P
    :R :input;

    input "q" =!
} W
