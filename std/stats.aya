.# This file is a part of Aya: https://github.com/nick-paul/aya-lang

module ::stats
export ::stats

from ::math import ::cumsum

.#? ::list stats.mean\n  average of a list of numbers
def stats::mean {
    .E\S\/
}


.#? ::list stats.variance\n  population variance
def stats::variance {set : m stats^,
    set stats.mean:m;
    set#{m-2^}S setE /
}


.#? L stats.stdev\n  standard deviation
def stats::stdev {: stats^,
    stats.variance .^
}


.#? ::list stats.geomean\n  geometric mean
.# the nth root of the product of n numbers
.# compute by summing the logarithms of the values
def stats::geomean {
    Ml.E\S\/Me
}


.#? L stats.median\n  median value of a list
def stats::median {l,
    lC$E2/.\        .# Sort the list and get its middle index
    lE2%0= {        .# If the list has even length,
        $1-KIS2/    .# average the middle two
    } {
        I           .# else, take the middle item
    }  .?
}


.#? ::list ::list stats.regression\n  return a block that evaluates to the regression of the sets
def stats::regression {x y :
    n a b
    sumy sumx
    sumxsq
    sumxytimes,

    xE:n;
    x2#^S:sumxsq;
    [x y,*]S:sumxytimes;
    xS:sumx;
    yS:sumy;


    sumy sumxsq *  sumx sumxytimes * -
    n sumxsq * sumx2^ -
    /:a;

    n sumxytimes * sumx sumy * -
    n sumxsq * sumx2^-
    /:b;

    {b * a +} [::a ::b] .+
}


.#? ::list ::list stats.pearson\n  pearson correlation coefficient between 2 datasets
def stats::pearson {x y : n xs ys,
    xE:n;
    xS:xs;
    yS:ys;

    [x y,*]Sn* xs ys*-
     x2^Sn* xs2^ -
     y2^Sn* ys2^ -
     * .^
    /
}

def stats::hist {l::list nbins::num : min max bin_size bins counter,
    .# compute bin size using list min and max values
    l.min :min;
    l.max :max;
    max min - nbins / :bin_size;

    .# generate list of bins
    .# for example, if 2:bin_size, 1:min, 5:nbins
    .#    [ 3 5 7 9 11 ] :bins
    bin_size nbins L cumsum min + :bins;

    .# Set last bin to max value (prevent rounding errors)
    max bins.:[:1];

    .# Create a list of counters
    [nbins,; 0] :counter;

    .# Map each item to it's bin index
    l #{bins>S}
    .# count all bin indices
    :# {i, counter.[i] 1 + counter.:[i]};

    counter
}
