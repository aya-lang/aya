import ::color

module terminal
export ::terminal

def terminal::fg (color.colors.white)
def terminal::bg (color.colors.black)

def terminal::clear {"\{x1b}[2J".P}
def terminal::clearline {"\{x1b}[1:J\{x1b}[1G".P}

def terminal::_setfgstr {c::color, "\{x1b}[38;2;$(c.r);$(c.g);$(c.b)m" }
def terminal::_setbgstr {c::color, "\{x1b}[48;2;$(c.r);$(c.g);$(c.b)m" }

def terminal::setfg {c::color : terminal^,
    c terminal.:fg;
    c terminal._setfgstr .P
}

def terminal::setbg {c::color : terminal^,
    c terminal.:bg;
    c terminal._setbgstr .P
}

def terminal::bold      { "\{x1b}[1m" "\{x1b}[22m" .? .P}
def terminal::italic    { "\{x1b}[3m" "\{x1b}[23m" .? .P}
def terminal::underline { "\{x1b}[4m" "\{x1b}[24m" .? .P}
def terminal::strike    { "\{x1b}[9m" "\{x1b}[29m" .? .P}

def terminal::reset_font {: terminal^ ,
    0 terminal.bold
    0 terminal.italic
    0 terminal.underline
    0 terminal.strike
}

def terminal::reset_color {: color^ terminal^ ,
    color.colors.white terminal.setfg
    color.colors.black terminal.setbg
}

def terminal::replcolor {c::color : color^ terminal^ ,
    [c terminal._setfgstr
     "aya> "
     color.colors.white terminal._setfgstr]S
    :{sys.alterprompt}
}

def terminal::print_color {value c::color : terminal^ prev_fg,
    terminal.fg :prev_fg;
    c terminal.setfg
    value P .P
    prev_fg terminal.setfg
}

def terminal::println_color {value c::color : terminal^,
    value c terminal.print_color
    "" :P
}
