.# This file is a part of aya: https://github.com/aya-lang/aya

.# Defines the enum keyword which allows for
.#  easy creation of enum classes and symbol groups

{class::sym enums::list : dict meta,

    .# The metatable for each enum member
    :{
        class :__type__;
        1 :__pushself__;
        {self, "$(self.__type__:C).$(self.name:C)"}:__str__:__repr__;
        {other self,
            other.__type__ self.__type__ =
            other.name self.name = &
        }:__eq__;
    }:meta;

    .# The enum dict
    :{ class:name }:dict;

    .# Add each element to the enum dict
    enums # {e,
        :{e:name} meta :M e dict :D ;
    };

    .# Create and add the metatable for the enum
    dict :{
        1 :__pushself__;
        ::enum :__type__;
        {.name:C}:__repr__:__str__;
    } :M

    .# Assign the name of the enum to the enum class
    class := ;
}:_enum_impl;


.# enum macro

{ : _enum_impl(_enum_impl.`),
    { : _enum_impl^ , :&.[0]:S~ \.[1]~.*:#{:S~} _enum_impl } 2 :`
}:enum;
