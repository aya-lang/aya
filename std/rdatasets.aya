import ::csv


module rdatasets
export ::rdatasets

def rdatasets::_index_csv "https://raw.githubusercontent.com/vincentarelbundock/Rdatasets/master/datasets.csv"
def rdatasets::_cache_loc "rdatasets_index.csv"

.# Load index file
def rdatasets::load_index {:index_str("") data index(:{}) packages(:{}),

    {
        rdatasets._cache_loc G :index_str;
    } {
        "Downloading dataset from $(rdatasets._index_csv)..." :P
        rdatasets._index_csv G :index_str;
        index_str rdatasets._cache_loc 0 .G
    } .K

    "Parsing CSV..." :P
    index_str "," csv.parse :data;
    data V; :data; .# Remove first row

    data :d;

    .# Build index
    "Building index..." :P
    data :# {row : pkg(row.[0]) name(row.[1]),
        .# Add to url index
        row.[-2] index.:["$pkg/$name"]

        .# Add to package index
        .# Add package list if it does not exist
        packages pkg H ! { [] packages.:[pkg]; } ?
        .# Add name to list
        name packages.[pkg] .B ;

    };
    "Done." :P

    def rdatasets::index index
    def rdatasets::packages packages
}


def rdatasets::index :{}
def rdatasets::packages :{}

def rdatasets::load {id::str : rdatasets^ csv^,
    rdatasets.index :E 0 = {
        rdatasets.load_index
    } ?
    
    "/" id.in {
        rdatasets.index.[id]
    } {
        rdatasets.index.["datasets/$id"]
    } .?

    csv.open
}


rdatasets:r;




