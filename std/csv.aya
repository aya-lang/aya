{dlm : rgx,
    "(?<=^|$dlm)(\"(?:[^\"]|\"\")*\"|[^$dlm]*)" :rgx; .#"
    '\n' S :# {line,
        line.trim :line;
        line rgx & :#{.trim .rmquote .!}
    }
} :parse;

{f : parse^,
    fP G "," parse
} :open;

{kwargs::dict : parse^
                                clabel(1) .# bool
                                rlabel(::nil) .# bool
                                filename(::nil) .# str
                                csv_str(::nil) .# str
                                dlm(",")
                                data
                                colnames(::nil) .# ::list
                                rownames(::nil) .# ::list
                                ,
    kwargs ~

    (csv_str ::nil = filename ::nil = &) {"Must provide either filename or csv_str" .D} ?

    .# Open the file and read into data
    filename ::nil =! {filenameP G :csv_str;} ?

    csv_str dlm parse :data;

    .# Attempt to auto-detect rlabel if it is not specified
    rlabel ::nil = {
        data.[0].[0] "" = {
            1 :rlabel;
        } {
            0 :rlabel;
        } .?
    } ?

    .# CSV has column headers
    clabel {
        data V :colnames; ;
        .# If there are row labels, the first entry is for the index colum
        rlabel {colnamesV;:colnames;} ?
    } ?

    rlabel {
        data :#{V\;} :rownames;
    } ?

    :{
        data:data
        colnames:colnames
        rownames:rownames
    }
} :_read_kw;


{ arg : _read_kw^ ,
    {
        (arg str :@) {
            .# convert to dict
            :{ arg :filename }
        } (arg dict :@) {
            arg
        } {
            "Unsupported arg type $(arg:T). Must be ::str or ::dict" .D
        }
    } :?

    _read_kw
} :read;


.#? list sep na csv.dumps\n  convert a 2d list to a csv string
{list sep na,
    list :# {
        :#{
            .# If it is na, make it emptystr
            :& na = {;""} ?
            .# If it is a string, surround with quotes
            :& P \ str :@ {"\""+ "\""\+} ?
        }
        sep %
    } "\n" %
} :dumps;


