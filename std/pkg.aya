import json

{name::str,
    [:(sys.ad) :9s "pkg" :9s name ".aya" +] W :(sys.abspath)
} :pkg_path;

{name::str : pkg_path^ test_dir,
    [name pkg_path :9s "test"] W :test_dir;

    test_dir :(sys.isdir) {
        test_dir :(sys.readdir) :# {fname,
            fname ".aya" H {
                :{ .# open new scope
                    [test_dir :9s fname] W 
                    $ "Running test: " \+ :P
                    :F
                };
            } ?
        };
    } {
        "Package $name does not have a test directory" :P
    } .?

} :test;

{name::str : pkg_path^ json^,
    [name pkg_path :9s "package.json"] W G json.loads
} :metadata;

{name::str,
    :{ .# open a new scope
        name [::__run__] __aya__.importlib.require

        __run__
    };
} :run;

.# stub: username/pkgname
{stub::str : json^ repo_data branch pkg_data,
     "https://api.github.com/repos/$stub" G json.loads :repo_data;
     repo_data.default_branch :branch;
     "https://github.com/$stub/archive/refs/heads/$branch.zip":zip_url;
     "Downloading file: $zip_url ..." :P
     zip_url "pkg/tmp.zip" :(download.to_file)

    "Reading package information..." :P
    "pkg/tmp.zip" "pkg/tmp" :(sys.unzip)
    "pkg/tmp" $ :(sys.readdir).[0] \:9s+\+ :9s + "package.json" + G json.loads :pkg_data;

    "Installing $(pkg_data.name)..." :P
    "pkg/tmp" $ :(sys.readdir).[0] \:9s+\+ "pkg/" pkg_data.name + :(sys.mvdir)

	"Cleaning up..." :P
	"pkg/tmp" :(sys.rm)
	"pkg/tmp.zip" :(sys.rm)

} :add;
