.# This file is a part of Aya: https://github.com/nick-paul/aya-lang

.# turtle.aya
.# Defines the turtle type

import ::list
import ::plot
import ::math

.{? type: turtle
    A turtle. Used for drawing patterns on a plot grid.
.}
{,

    ::turtle :__type__;

    {{,0:x 0:y pi2/:r [[0 0]]:path} turtle MO}:__new__;


    {self,
        "turtle: pos=($(self.x), $(self.y)) dir=$(self.r)"
    }:__repr__:__str__;


    .#? N turtle.fwd\n  move the turtle forward N units
    {len self,
        self.x self.r Mc len* + self.:x;
        self.y self.r Ms len* + self.:y;
        [self.x self.y] self.path .V;
    }:fwd;

    .#? N turtle.left\n  turn the turtle left N radians
    {rads self,
        self.r rads + self.:r;
    }:left;

    .#? N turtle.right\n  turn the turtle right N radians
    {rads self,
        self.r rads - self.:r;
    }:right;

    .#? turtle.draw\n  draw the turtle's path
    {self : x y p small big margin,
        plot!:p;
        self.path .T ~\ :x;:y;
        x p.domain
        y p.addline

        x.max y.max .< :big;
        x.min y.min .> :small;
        big 0.1* :margin;

        [small margin- big margin+] p.:xaxis;
        [small margin- big margin+] p.:yaxis;

        p.view

    }:draw;
}:turtle;
