import "list"
import "plot"
import "math"

.{? type: turtle
    A turtle. Used for drawing patterns on a plot grid.
.}
{,

  ::turtle :type;

  {{,0:x 0:y pi2/:r [[0 0]]:path} turtle MO}:new;
  {self, "{turtle:$(self.x),$(self.y),$(self.r rad)\{deg}}"}:show;

  .#? N turtle.fwd\n  move the turtle forward N units
  {len self,
    self.x self.r Mc len* + self.:x;
    self.y self.r Ms len* + self.:y;
    [self.x self.y] self.path .V;
  }:fwd;

  .#? N turtle.left\n  turn the turtle left N radians
  {rads self,
    self.r rads + self.:r;
  }:left;

  .#? N turtle.right\n  turn the turtle right N radians
  {rads self,
    self.r rads - self.:r;
  }:right;

  .#? turtle.draw\n  draw the turtle's path
  {self : x y p small big margin,
    plot!:p;
    self.path transpose ~\ :x;:y;
    x p.domain
    y p.addline

    [x max y max] max :big;
    [x min y min] min :small;
    big 0.1* :margin;

    [small margin- big margin+] p.:xaxis
    [small margin- big margin+] p.:yaxis

    p.view

  }:draw;
}:turtle;



{,

  .#? turtledemo.ribbon\n  a ribbon
  {:frank,
    turtle! :frank;
    0 :count;
    {
      1 frank.fwd
      count Ms frank.right
      0.01 count +: count;
    } 1200 %
    frank.draw
  }:ribbon;

  .#? turtledemo.flowe\n  a simple flower
  {:t,
    turtle!:t;
    for 'i (12R) {
      for 'j (12R) {
        1 t.fwd
        30 deg t.right
      };
      30 deg t.right
    };
    t.draw
  }:flower;

  .#? turtledemo.snowflake\n  a snowflake
  {:t,
    turtle!:t;
    for 'i (24R) {
      for 'j (36R) {
        1 t.fwd
        170 deg t.right
      };
      20 deg t.right
    };
    t.draw
  }:snowflake;

}:turtledemo;

{:choice,
  turtledemo :K $
  "Select a Demo" "Turtle Demo" 1 6 MV :choice;

  "turtledemo." choice + ~

}:turtledemos;
