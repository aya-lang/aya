.# This file is a part of aya: https://github.com/aya-lang/aya

.# Metatable definition for the built-in `num` type

:{

    .#? ::num .digits\n  convert a number into a list of digits
    {
        Z           .# Convert to bignum
        .|          .# Absolute value (ignore negative)
        P B;V;      .# Remove the ': and 'z
        '. S V \;   .# Remove any decimal
        #:'         .# Convert to list of ascii values
        48-         .# Subtract 48 (ascii value of '0)
    }:digits;


    .#? ::num .bits\n  return binary representation of the integer
    {.| 10 2 .&}:bits;

    .#? ::num .bytes\n  return byte list representation of the integer
    {10 0 .&}:bytes;


    .#? ::num .round\n  round a decimal to the nearest whole value
    {0.49999999-./}:round;


    .#? ::num ::num.fixed\n  round N1 to N2 decimal digits
    {E$@@*.\\/}:fixed;


    .#? n::nun lo::num hi::num .clip N1 between N2 (lo) and N3 (hi)
    .# Reverse the order of min/max algorithms depending on sign
    {@ .> .<}:clip;


    .#? ::num.i\n  generate a range from -N to N
    .{ Example:
        aya> 4.i
        [ -4 -3 -2 -1 0 1 2 3 4 ]
        aya> 1.i
        [ -1 0 1 ]
        aya> -1.i
        [ 1 0 -1 ]
    .}
    {
        $           .# dup n so we can use the sign later
        .| $KR\R    .# create negative and positive versions of the list
        0\:J:J        .# join lists with a 0 between
        \0<{U}?     .# If the sign is negative, reverse
    }:i;


    .#? ::num.frac\n  the fractional part of the number
    { 1:% }:frac;


    .#? ::num.s\n  split a decimal into two numbers
    .{ Example:
        aya> 4.6 .s
        4 6
        aya> 0.3 .s
        0 3
        aya> 5 .s
        5 0
    .}
    {
        $1:%P$.[0]'.={V;}?~ \.\ \
    }:fracsplit;

}
.# Merge with num metatable
0 .M \.+

