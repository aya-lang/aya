.# This file is a part of aya: https://github.com/aya-lang/aya

.# Metatable definition for the built-in `char` type

:{
    .#? C.islower\n  tests if a character is lowercase
    {$ 'a :> \ 'z :< &}:islower;

    .#? C.isupper\n  tests if a character is uppercase
    {$ 'A :> \ 'Z :< &}:isupper;

    .#? C.toupper\n  converts a character to uppercase
    {$.isupper! {!} ? }:upper;

    .#? C.tolower\n  converts a character to lowercase
    {$.islower! {!} ? }:lower;

    .#? C.isdigit\n  tests if a character is a digit
    {$ '0 :> \ '9 :< &}:isdigit;

    .#? C.iswhitespace\n  tests if a character is a whitespace character
    { " \f\n\r\t\v\{x00A0}\{x2028}\{x2029}" \ H}:iswhitespace;

    .#? C.isascii\n  test if character is ascii
    {:' 127 <}:isascii;

    .#? C.isalpha\n  test if character is alphabetical
    {$ $ 'a :> \ 'z :< & \ $ 'A :> \ 'Z :< & |}:isalpha;

    .#? ::str ::char .split_keep\n  split the string at the char, keep the char in the leading string
    {s c,
        s c S #{c+} 

        .# If c is not the last char of s,
        .# remove c from the last item of the list
        s.[-1]c=! {
            :&.[-1]B;\.:[-1]
        } ? 
        
    }:split_keep;
}
.# Merge with char metatable
'x .M \.+
