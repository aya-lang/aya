

.{? type: date
    The date type provides operations for accessing date and time variables.
.}
{,

  ::date :type;

  {{,M$:ms} date MO}:now;

  {in, [
    in:T ::num = {{,in:ms} date MO} ?
    in:T ::str = {{,in "yyyy-MM-dd" MH:ms} date MO} ?
    {"Expected S|D, recieved: $in".D}
  ].S }:new;

  {.ms "MMM dd, yyyy h:mm:ss a" Mh}:repr;
  {.repr}:str;

  {.msMD0I}:dayofweekid;
  {.msMD0I dates.weekdays\V I}:dayofweek;
  {.msMD0I dates.weekdaysabb\V I}:dayofweekabb;
  {.msMD1I}:year;
  {.msMD2I}:monthid;
  {.msMD2I dates.monthnames\I}:month;
  {.msMD2I dates.monthsabb\I}:monthabb;
  {.msMD3I}:dayofmonth;
  {.msMD4I}:hour:h;
  {.msMD5I}:min:m;
  {.msMD6I}:sec:s;
  {.ms"a"Mh}:ampm;

  {unit self,
    self.ms unit.ms + self.:ms;
    self
  }:add;

  {unit self,
    self.ms unit.ms - self.:ms;
    self
  }:sub;

  {.ms}:dollar;

  .# Parse Functions
  {"MM/dd/yy"MH}:parsemdy;

  .# String functions
  {.ms"MM/dd/yy"Mh}:mmddyy;
  {.ms"MM/dd/yyyy"Mh}:mmddyyyy;
  {d, "$(d.h):$(d.m)"}:timestr;
}:date;

.#? dateunit\n  static namespace defining units related to dates
{,
  [
    ["year" 31536000000]
    ["month" 2628000000]
    ["week" 604800000]
    ["day" 86400000]
    ["hour" 3600000]
    ["minute" 60000]
    ["second" 1000]
  ]:units;

  for 'u units {:name conv,
    u0I:name;
    u1I:conv;
    "
    {,
      {n::num, {,n:n} dateunit.$name MO}:new;
      {.n\" dates.$name\"+}:show;
      {.n $conv*}:ms;
      {\+}:add;
      {\-}:sub;
    }:$name;
    "~
  };
}:dateunit;

.#? dates\n  static namespace for date related variables
{,
  .# Units
  for 'u (dateunit.units) {:name,
    u0I:name;
    "{dateunit.$name!}:$name"~
  };

  .# Week Day Names
  ["Sunday" "Monday" "Tuesday" "Wednesday"
   "Thursday" "Friday" "Saturday"]:weekdays;
  ["Sun" "Mon" "Tue" "Wed" "Thr" "Fri" "Sat"]:weekdaysabb;

  .# Month Names
  ["January" "February" "March" "April"
   "May" "June" "July" "August"
   "September" "October" "November" "December"
  ]:monthnames;

  ["Jan" "Feb" "Mar" "Apr" "May" "Jun"
   "Jul" "Aug" "Sep" "Oct" "Nov" "Dec"
  ]:monthsabb;

}:dates;

{date.now}:now;
