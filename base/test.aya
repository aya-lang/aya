.# This file is a part of Aya: https://github.com/nick-paul/aya-lang


.# Test object
{,
  ::test :type;

  .# Create a new test
  .#   blk:      the stack to be evauated
  .#   expected: the state of the stack after evaluation
  .#             ex: {1 2 + 3 [4]} => [3 3 [4]]
  {blk::block expected::list,
    {, "blk".Z:blk expected:expected } test MO
  }:new;

  .# Run the test. If passed, return "", else return error message
  {self : res blk expected,
    self.W

    [blk] :res;
    res expected = ! {
      ["Test failed: $blk\n"
       "\tExpected: $expected\n"
       "\tRecieved: $res" ]S
    } { "" } .?

  }:run;

}:test;



{,
  ::testset :type;

  .# Create a new test set
  {name::str,
    {, name:name []:tests} testset MO
  }:new;

  .# Add a new test to the test set
  {blk::block expected::list self,
    "blk".Z expected test!
    self.tests
    K ;
  }:add;

  .# Evaluate all tests, if any failed, report
  {self,
    self.tests #{.run} [""] :|
    .E 0 = {
      ; "testset '$(self.name)': All tests passed!" :P
    } {
      "testset '$(self.name)': Tests failed!\n" :P
      #{:P}
    } .?
  }:run;
}:testset;
