.# This file is a part of Aya: https://github.com/nick-paul/aya-lang

.# plot.aya
.# Defines the plot type

import ::list

{, .# plot

    ::plot :__type__;
    1 :__pushself__;

    {, .# axis
        {min::num max::num,
            {,min:min max:max} plot.axis :M
        }:__new__;
    }:axis;


    .#? plot.new\n  create a new plot instance with default settings
    {meta : plt,
        .# [plot.fieldnamesE,0] plot MO :plt;
        {,
            ::line      :plottype
            500         :width
            400         :height
            ""          :title
            ""          :xlabel
            ""          :ylabel
            []          :x
            []          :y
            1           :legend
            :-1         :stroke .# Use individual strokes
        }
        meta :M
    }:__new__;

    .#? ::plot plot.view\n  view the plot in a new window
    {self::plot,
        self MX
    }:view;

    .#? S ::plot plot.save\n  save the plot as a PNG in the working directory with the name S
    {filename::str self : cpy,
        self$ :cpy;; .# Create a copy
        0 cpy.:show;
        filename  cpy.:filename;
        cpy MX
    }:save;

    .#? L<N> ::plot plot.addline\n  add a dataset to the plot using defaults
    {list::list self,
        "" 1 [] list self.addseries
    }:addline;

    .#? L<N> ::plot plot.domain\n  set the domain of the plot
    {data::list self,
        data self.:x;
    }:domain;

    .#? nameS strokeN colorL dataL ::plot plot.addseries\n  add a series to a plot
    {name::str stroke::num color::list data::list self,
    .# stroke 0 = then {self.stroke :stroke;}
        self.y {,
            name:name
            stroke:stroke
            color:color
            data:data
        } AK;
    }:addseries;

    .#? nameS strokeN colorL exprE ::plot plot.addexpr\n  evaluate the expression on the domain of the plot and add it as a series
    {name::str stroke::num color::list expr::block self,
        self.y {,
            name:name
            stroke:stroke
            color:color
            [self.x, expr]:data
        } AK;
    }:addexpr;

    .# domainL rangeL plot.scatter\n  return a scatter plot with the given domain and range
    {domain::list range::list : plt,
        plot!:plt;
        ::scatter plt.:plottype;
        domain plt.domain
        range plt.addline
        0.5 plt.stroke
        plt
    }:scatter;

    .#? domainL rangeL plot.scatterreg\n  plot a scatter plot and a regression plot. print the regression to the console
    {x::list y::list : reg,
        x y stats.regression :reg;
        x y plot.scatter.view
        x x#reg plot.line.view
        "Regression: y= $(1reg0reg-)x + ($(0reg))".P
    }:scatterreg;

    .# domainL rangeL plot.line\n  return a line plot with the given domain and range
    {domain::list range::list : plt,
        plot!:plt;
        domain plt.domain
        range plt.addline
        plt
    }:line;

    .#? munN maxN eE plot.fn\n  plot the function e on the domain (min, max). return a plot instance
    {min::num max::num ex::block : plt,
        plot!:plt;
        minMd maxMd 600.1 linspace plt.domain
        "" 1 [] {ex} plt.addexpr
        plt
    }:fn;

    .#? minN maxN exsL plot.fns\n  plot each function in exs on the domain (min, max). return a plot instance
    {min::num max::num exs::list : plt,
        plot!:plt;
        minMd maxMd 600.1 linspace plt.domain
        exs # {x,
            "" 1 [] {x} plt.addexpr
        };
        plt
    }:fns;

    {c::color : plt,
        plot! :plt;
        [1 2 2 1 1] plt.domain
        "" 500 c.rgb [1 1 2 2 1] plt.addseries
        100 plt.:width;
        100 plt.:height;
        [1 2] plt.:yaxis;
        [1 2] plt.:xaxis;
        plt.view
    }:showcolor;

    {cs::list : plt i(0),
        plot! :plt;
        100 plt.:width;
        100 10 csE* + 100 400.clip plt.:height;
        [1 2] plt.domain
        cs :# {c,
            "" 10 c.rgb [i i] plt.addseries
            ::i B
        };
        [1 csE] plt.:yaxis;
        [1 2] plt.:xaxis;
        plt.view
    }:showcolors;

    .#? ::list lineplot\n  draw a quick lineplot of the list
    {
        .# Generate a plot object
        :& .ER\plot.line
        .# Compute 2% margin on top and bottom
        \:&.max \.min\K :&{-}F0.02*.|[1|$T\]+ \.:yaxis
        .# Display the plot
        .view
    }:lineplot;

    {"Plot Instance"}:str:repr;

}:plot;


def list::x {l::list,
    l E 2 = {l#:T [::list ::list]= {1} {0} .?} {0} .? {
        l.[0] l.[1] plot.line.view
    } {
        l plot.lineplot
    } .?
}

.# Basic Plotting

.{
{.X}:plot;
[(-10)$0.1+10]R:plotrange:pr;             .#? plotrange pr \n  a range from -10 to 10 at 0.1 increments
{fE,[(-10)$.01+10,f].X}:quickplot;        .#? E quickplot qp \n  plots an expression on the testplot range
{fE,[(-10)$.01+10,f,0=!].X}:plotnozero;   .#? E plotnozero \n  same as plot but with no zero (avoids div by zero error)
{$(-2)E+}:dy:ddelta;                      .#? N dt or \{delta} \n  copies the value and adds one-hundredth\n ex: [0dt1,Ms].X
{(-2)E+}:dx;                              .#? N dx \n  adds one one-hundredth to the value
.}



.{? scatter plot example
  [10,;.Q] [10,;.Q] plot.scatter :p;
  "A Scatter plot" p.:title
  p.view
.}

.{? line plot example
  10R $#Mq plot.line :p;
  3 p.:stroke
  p.view
.}
